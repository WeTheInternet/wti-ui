
pluginManagement {
    String xapiRepoDefault = "<default>"
    String repo = xapiRepoDefault
    try { repo = "${xapiRepo}" }
    catch (ignored) {}
    if (repo == xapiRepoDefault) {
        repo = "$rootDir.parent/xapi/repo"
        settings.ext.xapiRepo = repo
        logger.quiet "No xapiRepo set in ~/.gradle/gradle.properties, using xapiRepo=$repo"
    }
    repositories {
        gradlePluginPortal()
        mavenCentral()
        google()
        maven {
            name = 'xapiLocal'
            url = new URI("file://${xapiRepo}")
        }
    }
    plugins {
        id "${quarkusPluginId}" version "${quarkusPluginVersion}"
        id "io.quarkus.extension" version "${quarkusPluginVersion}"
        id "xapi-settings" version "${xapiVersion}"
    }
}
buildscript {
    repositories {
        maven {
            name = 'xapiLocal'
            url = new URI("file://${xapiRepo}")
        }
    }
    dependencies {
        classpath 'net.wti.gradle.modern:xapi-gradle-settings-plugin:0.5.1'
    }
}

apply plugin: 'xapi-settings'

rootProject.name = 'wti-ui'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // we define all our version information in .toml file and access as `libs.xapi.whatever` in a typesafe manner
            from(files('gradle/wti-ui-versions.toml'))
        }
    }
}

