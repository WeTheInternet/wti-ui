plugins {
    // GenInclude plugin from file:///opt/wti-ui/gdx-themes/src/main/gradle/plugins
    id "net.wti.gradle.font" version "$xapiVersion"
    id "net.wti.gradle.atlas" version "$xapiVersion"
    id	"java-library"
    id	"maven-publish"
}

// GenStart net.wti.gradle.settings.plugin.XapiSettingsPlugin
ext.xapiModern = 'true'
java.toolchain.languageVersion = JavaLanguageVersion.of(8)
repositories.mavenCentral()
// setup sourcesets

SourceSet main = sourceSets.maybeCreate('main');

SourceSet test = sourceSets.maybeCreate('test');
main.java.setSrcDirs([]);
main.resources.setSrcDirs([]);
test.java.setSrcDirs([]);
test.resources.setSrcDirs([]);
main.java.srcDir("$rootDir/gdx-themes/src/main/java")
main.resources.srcDir("$rootDir/gdx-themes/src/main/resources")

dependencies {
    api project(path: ":wti-ui-view")
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api libs.xapi.fu
}

repositories {
    maven {
        name = 'xapiLocal'
        url = "/opt/xapi/repo"
    }
}
// Setup publishing to coordinates: net.wti:gdx-themes
project.extensions.add('xapi.mvn.repo', "/opt/xapi/repo")
Task xapiPublish = tasks.create('xapiPublish')
xapiPublish.group = 'Publishing'
xapiPublish.description = 'Publish jars to xapiLocal repository'
xapiPublish.dependsOn "publishXapiPublicationToXapiLocalRepository"

PublishingExtension ext = extensions.findByName(PublishingExtension.NAME) as PublishingExtension
ext.repositories.maven {
    MavenArtifactRepository repo ->
        repo.name = 'xapiLocal'
        repo.url = "file:///opt/xapi/repo"
}
// not really what we want, but it's good enough for right now
java.withSourcesJar()
java.withJavadocJar()

ext.publications.create("xapi", MavenPublication, {
    pub ->
        afterEvaluate {
            if (tasks.names.contains('shadowJar')) {
                shadow.component(pub)
            } else {
                pub.from(components.named('java').get())
            }
        }
        pub.artifactId = "gdx-themes"
        pub.groupId = "net.wti"
})
// GenEnd net.wti.gradle.settings.plugin.XapiSettingsPlugin
// GenInclude body.end from file:///opt/wti-ui/gdx-themes/src/main/gradle/body.end

// to regenerate fonts, run ./gradlew generateFonts

xapiFonts {
    outputName = 'wti-fonts'
    googleFont 'Questrial', {
        charFile = file("$rootDir/text-chars.txt")
        addSizedWeight 72, 72, 'Regular', 8
        addSizedWeight 96, 96, 'Regular', 12
        addSizedWeight 132, 132, 'Regular', 18
        addSizedWeight 160, 160, 'Regular', 24
    }
    googleFont 'NotoSansSymbols2', {
        charFile = file("$rootDir/noto-sans-symbol-2-chars.txt")
        // with only three symbols, these can take up very little space
        addSizedWeight 140, 24, 'Regular', 18
        addSizedWeight 182, 30, 'Regular', 24
    }
    googleFont 'NotoEmoji', {
        extraUrl = '[wght]'
        charFile = file("$rootDir/noto-sans-emoji-chars.txt")
        // with only three symbols, these can take up very little space
        addSizedWeight 46, 34, 'Regular', 24
        addSizedWeight 52, 44, 'Regular', 32
    }

    googleFont 'PlayfairDisplay', {
        extraUrl = '[wght]'
        charFile = file("$rootDir/playfair-display-chars.txt")
        addSizedWeight 26, 16, '', 32
    }
}
def packFonts = tasks.named('packFonts')
tasks.register('copyFonts', Copy) {
    from(project.provider({packFonts.get().outputs.files}))
    into('resources/cc-by-4/wti/common/')
    dependsOn packFonts
    duplicatesStrategy = 'INCLUDE'
}
packFonts.configure {
    finalizedBy 'copyFonts'
}
xapiAtlas {
  atlasName = 'wti-common-ui'
  // adjust if you prefer a different output path:
  outputDir = layout.projectDirectory.dir('resources/cc-by-4/wti/common')

  // ------------------------------------------------------------------
  // 1) Utility pixel
  // ------------------------------------------------------------------
  pixel 'white', '#FFFFFFFF'

  // ------------------------------------------------------------------
  // 2) Tooltip background (scalable 9-patch)
  //    Names produced: tooltip-bg
  // ------------------------------------------------------------------
  ninePatch('tooltip-bg') {
    width = 16; height = 16

    // Scalable nine-patch margins (left, right, top, bottom)
    // Previously: split 4, 4, 4, 4
    stretch 4, 4, 4, 4

    // Content padding applied by Scene2D layout (top, left, bottom, right)
    padTop = 6; padLeft = 8; padBottom = 6; padRight = 8

    // Base fill; opaque slate
    fillHex = '#2a2a2aff'

    // Optional: extra inset to keep AA off guides
    // inset 0.5f
  }

  // ------------------------------------------------------------------
  // 3) General button family (normal)
  //    Names produced:
  //      button, button-over, button-pressed, button-disabled
  // ------------------------------------------------------------------
  family('button') {
    size 52, 50
    corner 8
    // splits (L,R,T,B) – center stretches
    split 8, 8, 7, 8
    // content pad (T,L,B,R)
    pad 4, 4, 5, 4
    // base look
    fill '#2C2F3AFF'
    gradient 0.12f, -0.08f
    stroke '#00000066', 1
    innerStroke '#FFFFFF10', 1
    shadow 1, '#00000040'

    state('default') { /* -> "button" */ }
    state('over')    { gradient 0.18f, -0.05f }             // -> "button-over"
    state('pressed') { gradient 0.06f, -0.18f; padDelta 1,0,-1,0 } // -> "button-pressed"
    state('disabled'){ alpha 0.55f }                        // -> "button-disabled"
  }

  // ------------------------------------------------------------------
  // 4) Emphasis button (slimmer)
  //    Names produced:
  //      button-emphasis, button-emphasis-over,
  //      button-emphasis-pressed, button-emphasis-disabled
  // ------------------------------------------------------------------
  variant('button-emphasis', 'button') {
    size 51, 31
    // tuned for the slimmer height
    split 8, 7, 9, 10
    pad   7, 9, 4, 6
    // slight brightness bump for emphasis base
    gradient 0.16f, -0.08f
  }

  // ------------------------------------------------------------------
  // 5) Colored emphasis (blue) – same states as emphasis
  //    Names produced:
  //      button-emphasis-color, ...-over, ...-pressed, ...-disabled
  // ------------------------------------------------------------------
  variant('button-emphasis-color', 'button-emphasis') {
    fill '#3452B8FF'        // deep blue
    gradient 0.18f, -0.10f
  }

  // ------------------------------------------------------------------
  // 6) Calendar / recurrence cells
  //    Names produced:
  //      cell-inactive, cell-active, cell-hover, cell-selected
  // ------------------------------------------------------------------
  family('cell') {
    size 24, 24
    corner 6
    split 4, 4, 4, 4
    pad 2, 2, 2, 2

    // shared base look
    fill '#23272EFF'
    gradient 0.10f, -0.05f
    stroke '#00000066', 1
    innerStroke '#FFFFFF10', 1

    state('inactive') { alpha 0.85f }                                      // -> "cell-inactive"
    state('active')   { fill '#313844FF'; gradient 0.12f, -0.08f }         // -> "cell-active"
    state('hover')    { gradient 0.16f, -0.02f }                           // -> "cell-hover"
    state('selected') { fill '#3A4252FF'; gradient 0.20f, -0.12f }         // -> "cell-selected"
  }

  // ------------------------------------------------------------------
  // 7) A slim vertical divider you can stretch in Y (shorter than full height in layout)
  // ------------------------------------------------------------------
    ninePatch('divider-vert') {
      width = 2; height = 16
      // make center stretchable
      stretch 1, 1, 6, 6
      // no content padding
      padTop = 0; padLeft = 0; padBottom = 0; padRight = 0
      fillHex = '#00000055'    // subtle
    }

    // Add a matching horizontal divider for vertical SplitPane handles
    ninePatch('divider-horiz') {
      width = 16; height = 2
      // make center stretchable
      stretch 6, 6, 1, 1
      // no content padding
      padTop = 0; padLeft = 0; padBottom = 0; padRight = 0
      fillHex = '#00000055'    // subtle
    }

  // ------------------------------------------------------------------
  // 8) A soft panel background for the action bar
  // ------------------------------------------------------------------
  family('panel-actionbar') {
    size 4, 4            // tiny base; ninepatch will scale
    corner 8
    split 6, 6, 6, 6
    pad   4, 6, 4, 6     // inner padding
    fill  '#2C2F3AFF'
    gradient 0.08f, -0.04f
    stroke '#00000066', 1
    innerStroke '#FFFFFF10', 1

    state('default') { } // exports: "panel-actionbar"
  }

  // ------------------------------------------------------------------
  // Global insetting (optional)
  // ------------------------------------------------------------------
  // defaultInsetPx = 0.5f
  // insetPrefix    = 'inset-'
}
// Done generating buildfile for :gdx-themes at file://$rootDir/gdx-themes/src/main/gdx-themesMain.gradle
